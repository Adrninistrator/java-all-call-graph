apply plugin: 'eclipse'
apply plugin: 'java'
apply plugin: 'idea'

apply from: 'unittest.gradle'

group 'com.github.adrninistrator'
version = "1.0.9"
def projectName = "java-all-call-graph"
description = "${projectName}"
sourceCompatibility = 1.8
targetCompatibility = 1.8
def jar_output_dir = "jar_output_dir"

def defaultEncoding = 'UTF-8'

[compileJava, compileTestJava]*.options*.encoding = defaultEncoding

project.buildDir = 'build'

repositories {
    mavenCentral()
}

clean {
    delete 'build'
    delete "${jar_output_dir}"
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }

        resources {
            srcDir 'src/main/resources'
        }
    }

    test {
        java {
            srcDir 'src/test/java'
        }

        resources {
            srcDir 'src/test/resources'
        }
    }
}

List apache_common = [
        'org.apache.commons:commons-lang3:3.12.0',
        'commons-io:commons-io:2.11.0',
        'commons-codec:commons-codec:1.15'
]

List druid = ['com.alibaba:druid:1.2.15']

List mysql = ['com.mysql:mysql-connector-j:8.0.31']

List h2database = ["com.h2database:h2:2.1.214"]

List slf4j = ['org.slf4j:slf4j-api:1.7.36']

List java_callgraph = ['com.github.adrninistrator:java-callgraph2:1.0.10']

List mybatis_parser = ['com.github.adrninistrator:mybatis-mysql-table-parser:0.0.5']

List jackson = ["com.fasterxml.jackson.core:jackson-databind:2.14.0"]

List jdom = ['org.jdom:jdom2:2.0.6.1']

List spring_jdbc = ['org.springframework:spring-jdbc:5.3.26']

List log4j = [
        'org.slf4j:log4j-over-slf4j:1.7.36',
        'org.apache.logging.log4j:log4j-api:2.19.0',
        'org.apache.logging.log4j:log4j-core:2.19.0',
        'org.apache.logging.log4j:log4j-slf4j-impl:2.19.0'
]

List junit = [
        'org.junit.platform:junit-platform-launcher:1.9.3',
        'org.junit.jupiter:junit-jupiter-engine:5.9.3',
        'org.junit.vintage:junit-vintage-engine:5.9.3'
]

List spring_for_test = [
        'org.springframework:spring-beans:5.3.26',
        'org.springframework:spring-context:5.3.26',
        'org.springframework:spring-web:5.3.26'
]

List javax_inject = ['javax.inject:javax.inject:1']

List spring_test = ['org.springframework:spring-test:5.3.26']

dependencies {
    compile apache_common, druid, mysql, h2database, slf4j, java_callgraph, mybatis_parser, jackson, jdom, spring_jdbc
    testImplementation log4j, junit, spring_for_test, javax_inject, spring_test
}

compileJava.options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"

processResources {
    setDuplicatesStrategy(DuplicatesStrategy.INCLUDE)
}

task createProject {
    doFirst {
        sourceSets*.java.srcDirs*.each { it.mkdirs() }
        sourceSets*.resources.srcDirs*.each { it.mkdirs() }
    }
}

jar {
    from(sourceSets.test.allJava) {
        include 'test/jacg/*.java'
        into '/'
    }
}

task test_jar(type: Jar) {
    archiveName 'test.jar'
    from(sourceSets.test.output) {
        include 'test/call_graph/**'
        exclude 'test/call_graph/other_jar/**'
        into '/'
    }
    from('src/test/resources/') {
        include 'applicationContext.xml'
        into 'resources/'
    }
}

// 生成可以直接执行的jar包，并拷贝相关文件
task gen_run_jar(type: Jar) {
    archiveName 'run_jacg.jar'
    from(sourceSets.main.output) {
        into '/'
    }
    from(sourceSets.test.output) {
        include 'test/jacg/**'
        into '/'
    }

    def jacgConfigDirs = new File('src/main/resources').listFiles().findAll { File f -> (f.isDirectory() && f.name.startsWith('_jacg_')) }

    doLast {
        copy {
            from 'build/libs/run_jacg.jar'
            into "${jar_output_dir}/jar/"
        }
        // 拷贝配置文件目录
        jacgConfigDirs.each { jacgConfigDir ->
            def dirName = jacgConfigDir.getName()

            copy {
                from 'src/main/resources/' + dirName
                into "${jar_output_dir}/" + dirName
            }
        }
        copy {
            from new File('bat').listFiles().findAll { File f -> (f.name.endsWith('.bat')) }
            into "${jar_output_dir}/"
        }
        copy {
            from new File('shell').listFiles().findAll { File f -> (f.name.endsWith('.sh')) }
            into "${jar_output_dir}/"
        }
        copy {
            from new File('src/test/resources').listFiles().findAll { File f -> (f.name.startsWith('log4j2') && f.name.endsWith('.xml')) }
            into "${jar_output_dir}/config/"
        }
        copy {
            from configurations.testRuntimeClasspath.getFiles().findAll {
                    /*
                        需要使用以下Spring相关的组件：
                        spring-beans
                        spring-core
                        spring-jcl
                        spring-jdbc
                        spring-tx
                     */
                File f ->
                    ((!f.name.startsWith('spring-') || f.name.startsWith("spring-beans-") || f.name.startsWith("spring-core-") || f.name.startsWith("spring-jcl-") ||
                            f.name.startsWith("spring-jdbc-") || f.name.startsWith("spring-tx-")) && !f.name.startsWith('junit-'))
            }
            into "${jar_output_dir}/lib"
        }
    }
}

idea {
    module {
        // and some extra dirs that should be excluded by IDEA
        excludeDirs += file('.idea')
        excludeDirs += file('_jacg_o_ee')
        excludeDirs += file('_jacg_o_er')
        excludeDirs += file('gradle')
        excludeDirs += file("${jar_output_dir}")
        excludeDirs += file('bat')
        excludeDirs += file('shell')
        excludeDirs += file('log')
        excludeDirs += file('_unittest_output')
    }
}
